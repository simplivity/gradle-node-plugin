apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.gradle.plugin-publish'

ext {
    bintrayUser = resolveProperty( 'BINTRAY_USER', 'bintrayUser' )
    bintrayKey = resolveProperty( 'BINTRAY_KEY', 'bintrayKey' )
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar
        }
    }
}

artifactoryPublish {
    onlyIf { isMaster }
    mustRunAfter 'build'
    publications 'mavenJava'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'

    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
            maven = true
        }
    }
}

pluginBundle {
    website = 'https://github.com/srs/gradle-node-plugin'
    vcsUrl = 'https://github.com/srs/gradle-node-plugin'

    plugins {
        nodePlugin {
            id = 'com.moowork.node'
            displayName = 'Gradle Node Plugin'
            description = 'Gradle plugin for executing node scripts.'
            tags = ['java', 'gradle', 'node', 'yarn', 'npm']
        }
        gruntPlugin {
            id = 'com.moowork.grunt'
            displayName = 'Gradle Grunt Plugin'
            description = 'Gradle pluign for executing grunt builds.'
            tags = ['java', 'gradle', 'node', 'yarn', 'npm', 'grunt']
        }
        gulpPlugin {
            id = 'com.moowork.gulp'
            displayName = 'Gradle Gulp Plugin'
            description = 'Gradle plugin for executing gulp builds.'
            tags = ['java', 'gradle', 'node', 'yarn', 'npm', 'gulp']
        }
    }

    mavenCoordinates {
        groupId = 'com.moowork.gradle'
        artifactId = 'gradle-node-plugin'
    }
}

String resolveProperty( String envVarKey, String projectPropKey )
{
    String propValue = System.getenv()[envVarKey]

    if ( propValue )
    {
        return propValue
    }

    hasProperty( projectPropKey ) ? getProperty( projectPropKey ) : null
}
